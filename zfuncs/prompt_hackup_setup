# a theme hacked up from many.
# by jacob peddicord (jmp.name)

function prompt_hackup_precmd {
	vcs_info
	print -Pn "\e]2;%n@%M\a"
}

function prompt_hackup_setup() {
	setopt LOCAL_OPTIONS
	prompt_opts=(cr percent subst)
	autoload -Uz add-zsh-hook
	autoload -Uz vcs_info
	add-zsh-hook precmd prompt_hackup_precmd

	zstyle ':vcs_info:*' enable bzr git hg svn
	zstyle ':vcs_info:*' check-for-changes true
	zstyle ':vcs_info:*' unstagedstr '!'
	zstyle ':vcs_info:*' stagedstr '+'
	zstyle ':vcs_info:*' actionformats "%b%u%c (%a) "
	zstyle ':vcs_info:*' formats "%b%u%c "

	PROMPT="%K{0}%F{4}%# ${SSH_CLIENT:+%n@%m }%F{11}%~%f %F{6}"'${vcs_info_msg_0_}'"%k
%F{9}%B%(0?,,%K{1}%F{0})>%b%k%f "
}

prompt_hackup_setup "$@"
